apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.ben-manes.versions'
//apply plugin: 'io.fabric'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.2'

    dexOptions {
        maxProcessCount 4
        preDexLibraries false
        javaMaxHeapSize "8g"
    }

    defaultConfig {
        defaultPublishConfig 'debug'

        applicationId "id.sch.smkn2soe"
        minSdkVersion 18
        targetSdkVersion 26
        testInstrumentationRunner "${applicationId}.runner.RxAndroidJUnitRunner"
        versionCode 1
        versionName '0.0.1'

        buildConfigField("String", "POKEAPI_API_URL",
                "\"${PokeapiApiUrl}\"")

        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        def commonTestDir = 'src/commonTest/java'
        test {
            java.srcDir commonTestDir
        }
        androidTest {
            java.srcDir commonTestDir
        }
    }

    lintOptions {
        textOutput "stdout"
        textReport true
        checkAllWarnings true
        warningsAsErrors true
        showAll true
        explainIssues true
        abortOnError false
        lintConfig file("$projectDir/lint.xml")
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'LICENSE.txt'
        exclude 'META-INF/license/LICENSE.base64.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/MANIFEST.MF'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
        }
    }
}

configurations.all {
    resolutionStrategy {
        force 'com.squareup.okio:okio:1.11.0'
        force "com.squareup.okhttp3:okhttp:3.5.0"
        force 'org.jetbrains.kotlin:kotlin-stdlib:1.1.2-3'
    }
}

apply from: 'dependencies.gradle'

dependencies {
    //----- Design Support (Default By Generator)
    implementation supportLibs
    //----- Retrofit (Default By Generator)
    implementation networkLibs
    //----- Core RxJava (Default By Generator)
    implementation rxJavaLibs
    //----- Google Play Library and Firebase (Default By Generator)
    implementation googlePlayLibs
    //----- Multidex
    implementation 'com.android.support:multidex:1.0.1'
    //----- Image Utils
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation('com.github.bumptech.glide:okhttp3-integration:1.4.0') {
        exclude group: 'glide-parent'
    }
    // ---- Butterknife (Default By Generator)
    implementation "com.jakewharton:butterknife:$versions.butterKnife"
    implementation "com.jakewharton.rxbinding:rxbinding-kotlin:$versions.rx_binding_version"
    implementation "com.jakewharton.rxbinding:rxbinding-support-v4-kotlin:$versions.rx_binding_version"
    implementation "com.jakewharton.rxbinding:rxbinding-appcompat-v7-kotlin:$versions.rx_binding_version"
    implementation "com.jakewharton.rxbinding:rxbinding-recyclerview-v7-kotlin:$versions.rx_binding_version"
    // ---- Logging (Default By Generator)
    implementation "com.jakewharton.timber:timber:4.5.1"
    //----- Design Support (Default By Generator)
    implementation 'com.android.support:support-v4:27.0.2'
    // ---- Dagger (Default By Generator)
    implementation "com.google.dagger:dagger:$versions.dagger"
    compileOnly 'org.glassfish:javax.annotation:10.0-b28' //Required by Dagger2
    // ---- APT dependencies (Default By Generator)
    kapt "com.jakewharton:butterknife-compiler:$versions.butterKnife"
    kapt daggerCompiler
    kaptTest daggerCompiler
    kaptAndroidTest daggerCompiler
    //----- Unit Testing
    testImplementation unitTestLibs
    androidTestImplementation androidTestsLibs
    //----- Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    //----- Network Logging
    debugImplementation "com.readystatesoftware.chuck:library:$versions.chuck"
    releaseImplementation "com.readystatesoftware.chuck:library-no-op:$versions.chuck"
    //----- Permission Android M++
    implementation 'com.karumi:dexter:4.2.0'
    //----- Rx Life Cycle
    implementation 'com.trello:rxlifecycle-components:1.0'
    //----- Bottom Navigation
    implementation 'com.aurelhubert:ahbottomnavigation:2.1.0'
    //----- Custom Font
    implementation 'uk.co.chrisjenx:calligraphy:2.3.0'
    //----- Image Util
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    //----- Download File Library
    implementation 'com.mani:ThinDownloadManager:1.4.0'
    //----- Advance Web View
    implementation 'com.github.delight-im:Android-AdvancedWebView:v3.0.0'
}

// Log out test results to console
tasks.matching { it instanceof Test }.all {
    testLogging.events = ["failed", "passed", "skipped"]
}